
#------------------------------------------------------------
#	S E M A N T I Q U E   -   C L A S S
#------------------------------------------------------------

#------------------------------------------------------------
#	S Y N T A X
#------------------------------------------------------------

syntax extension jsonlikeql_syntax {

#	M U T A T I O N     Q U E R I E S
#------------------------------------------------------------

rule <queryMutator> 
	? @lchar queryType
	?! @Query query
	? @FunctionsMap functionsMap
	{ 
	$identifier$ ?let @lstring name 

	# détection des paramètres

	@ObjectLV parameters = .default;
	select or
	<function_parameters> !? parameters
	end  

	#[listeDesFonctions searchKey !functionName ?function ]

	#if (checkParameters(!parameters !fct_mParameters) == true) then

	#end

	# détection des champs de la requete
 
	@FieldsMap fieldsF = .default;

	<fieldsMutator> !queryType !?fieldsF	 

	@lstring returnedType = .default

	getFunctionReturnedType( !name !functionsMap !? returnedType);
 
	query = @MutateQuery.new{ !name !@lchar.new{!queryType !@location.here}  !parameters !returnedType !fieldsF } 
}

rule <fieldsMutator> 
	? @lchar queryType 
	?! @FieldsMap fieldsF
	{
	
	${$ 
		@Query query = @Query.default;
		@LValue lvalue = .default;
		<fieldMutator> !queryType !?query !?lvalue

		if( [[[query name] string] length] > 0 ) then  
	 		[!? fieldsF insertKey ![query name] !lvalue] 
 		end

		select or
			$,$
			repeat
				query = @Query.default;
				lvalue = .default;
				<fieldMutator> !queryType !?query !?lvalue

				if( [[[query name] string] length] > 0 ) then  
			 		[!? fieldsF insertKey ![query name] !lvalue] 
		 		end
			while
			$,$
			end
		end
	$}$ 
}

rule <fieldMutator> 
	? @lchar queryType
	?! @Query query
	?! @LValue lvalue
	{
	$identifier$ ?let @lstring name

	@ObjectLV parameters = .default;
 
	@FieldsMap fields = .default; 
 
	$=$ <value_definition> !? lvalue
 
	query = @MutateQuery.new{ !name !@lchar.new{!queryType !@location.here}  !parameters !.default !fields }
}

}

#------------------------------------------------------------
#	S E M A N T I Q U E   -   F U N C T I O N S
#------------------------------------------------------------

override method @MutateQuery toSql 
	?!@string sql
	{

	@LocalError erreur = {};
	@string ErrorObjectValueNotAllowed = "ObjectValueNotAllowed";
	@string ErrorArrayValueNotAllowed = "ArrayValueNotAllowed";
	@string ErrorifArgRequiresStringValue = "If-Arg-Requires-String-Value";
	[!? erreur insertKey !.new{!ErrorObjectValueNotAllowed !@location.here}]
	[!? erreur insertKey !.new{!ErrorArrayValueNotAllowed !@location.here}]
	[!? erreur insertKey !.new{!ErrorifArgRequiresStringValue !@location.here}]

	sql = "";
	sql += "/* Requete MUTATE `"+name.string +"` */\n"

	@string table = ""; 

	[self getMainTable !? table ] 

	if ( [queryType char] == '+') then 
		sql += "INSERT INTO ";

		sql += "`"+ table +"` " ;

		@string fieldsStr = "";
		@string valuesStr = "";
		@uint count = [fields count];
		@uint i = 0;		 

		for () in fields do 
			fieldsStr += [lkey string] 		

			@string value = .default
			[lvalue toString !?value]

			valuesStr += value;				

			if( i < count -1) then 
				fieldsStr += ", ";
				valuesStr += ", ";
			end
			i++; 
		end

		sql += "( "+ fieldsStr +" ) "; 

		sql += "VALUES ("+ valuesStr +") "; 

	else
		sql += "UPDATE ";

		sql += "`"+ table +"` " ;

		@string fieldsStr = ""; 
		@uint count = [fields count];
		@uint i = 0;	

		for () in fields do
			@string value = .default
			[lvalue toString !?value]

			fieldsStr += [lkey string] + " = " + value;

			if( i < count -1) then 
				fieldsStr += ", "; 
			end
			i++;
		end

		sql += "SET " + fieldsStr+" ";

		@MemberList mlist = [parameters mLvalue]

		#si des paramètres ont été fournis
		@uint argsCount = [mlist length]
		if(argsCount > 0) then  
			for member in mlist do 
				if ( [[member mMember] mKey] == "if" ) then 
					@Member m = [member mMember]
					@LValue lv = [m mValue]
					if ( (lv is == @StringLV) == false) then
						[!? erreur insertKey !.new{!ErrorifArgRequiresStringValue !@location.here}]
					end
					############@LValue conditions = [[member mMember ] mValue]				 
					@string conditions = "";
					[lv getString !?conditions]

					sql += "WHERE "+conditions+" "
				end
			end
			#
		end

	end


		

	sql += ";\n";
}

